plugins {
    id 'java-platform'
    id 'maven-publish'
}
def ENV = System.getenv()

allprojects {
    def buildNumber = ENV.BUILD_NUMBER
    version = "${project.api_version}.${buildNumber ?: "0"}"
    group = project.maven_group
}

archivesBaseName='platform'

void addRepo(RepositoryHandler repositories) {
    def ENV = System.getenv()
    if (ENV.mvn_username) {
        repositories.maven {
            name = "Maven"
            url = uri("sftp://maven.sandboxpowered.org:22/sbxmvn/")
            credentials {
                username = ENV.mvn_username
                password = ENV.mvn_password
            }
        }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_14

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url 'https://maven.sandboxpowered.org' }
    }

    if (project.name != "base") {
        dependencies {
            api project(':base')
        }
    }

    // Add fabric.mod.json to api modules for fabric support (for some reason we have to do this)
    ext.genOutputDir = file("$buildDir/generated/resources")
    task generateFabricModJson {
        ext.outputFile = file("$genOutputDir/fabric.mod.json")
        outputs.file(outputFile)
        doLast {
            outputFile.text = /{"id": "sandbox-$project.name", "version": "$project.version"}/
        }
    }
    sourceSets.main.output.dir genOutputDir, builtBy: generateFabricModJson

    publishing {
        publications {
            mavenJava(MavenPublication) {
                afterEvaluate {
                    artifact jar
                }
            }
        }
        addRepo(repositories)
    }
}

dependencies {
    constraints {
        api project(":base")
        api project(":rendering")
        api project(":rendering-opengl")
        api project(":rendering-vulkan")
    }
}

publishing {
    publications {
        platform(MavenPublication) {
            groupId = project.maven_group
            artifactId = 'api'

            from components.javaPlatform
        }
    }
    addRepo(repositories)
}