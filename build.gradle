plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id 'java-platform'
    id 'maven-publish'
}
def ENV = System.getenv()

allprojects {
    def buildNumber = ENV.BUILD_NUMBER
    version = "${project.api_version}.${buildNumber ?: "0"}"
    group = project.maven_group
}

archivesBaseName = 'platform'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: "com.jfrog.bintray"

    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
        maven { url 'https://maven.sandboxpowered.org/' }
    }

    if (project.name != "base") {
        dependencies {
            api project(':base')
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version

                artifact jar
            }
        }
    }
    bintray {
        user = System.getenv("BINTRAY_USER")
        key = System.getenv("BINTRAY_KEY")
        publications = ['mavenJava']
        publish = true

        pkg {
            repo = 'API'
            name = project.name
            userOrg = 'sandboxpowered'
            licenses = ['MIT']
            vcsUrl = 'https://github.com/sandboxpowered/sandboxapi.git'
            publicDownloadNumbers = true
            githubRepo = 'sandboxpowered/sandboxapi'
            githubReleaseNotesFile = 'README.md'
            version {
                name = project.version
                desc = ''
                vcsTag = project.version
                released  = new Date()
            }
        }

    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    constraints {
        api project(":base")
        api project(":rendering")

        api project(":resources")
    }
}
bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    publications = ['platform']
    publish = true

    pkg {
        repo = 'API'
        name = 'api'
        userOrg = 'sandboxpowered'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/sandboxpowered/sandboxapi.git'
        publicDownloadNumbers = true
        githubRepo = 'sandboxpowered/sandboxapi'
        githubReleaseNotesFile = 'README.md'
        version {
            name = project.version
            desc = ''
            vcsTag = project.version
            released  = new Date()
        }
    }

}
publishing {
    publications {
        platform(MavenPublication) {
            groupId = project.maven_group
            artifactId = 'api'

            from components.javaPlatform
        }
    }
}
