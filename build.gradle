plugins {
    id 'java-platform'
    id 'maven-publish'
    id 'me.qoomon.git-versioning' version '3.0.0'
}
def ENV = System.getenv()

def releasesRepoUrl = "https://nexus.sandboxpowered.org/repository/maven-releases/"
def snapshotsRepoUrl = "https://nexus.sandboxpowered.org/repository/maven-snapshots/"

version = "${project.api_version}-SNAPSHOT"
gitVersioning.apply {
    branch {
        pattern = 'develop'
        versionFormat = '${version}'
    }
    branch {
        pattern = 'feature/(?<feature>.+)'
        versionFormat = '${feature}-SNAPSHOT'
    }
    tag {
        pattern = 'release/(?<tagVersion>[0-9].*)'
        versionFormat = '${tagVersion}'
    }
    commit {
        versionFormat = '${commit.short}'
    }
}

allprojects {
    group = project.maven_group
}

archivesBaseName = 'platform'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    version = rootProject.version

    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        maven { url 'https://nexus.sandboxpowered.org/repository/maven-public/' }
    }

    if (project.name != "base") {
        dependencies {
            api project(':base')
        }
    }

    ext.genOutputDir = file("$buildDir/generated/resources")
    def templateModJson = rootProject.file("template/module-fabric.mod.json")
    task generateFabricModJson {
        def outputFile = file("${project.ext.genOutputDir}/fabric.mod.json")
        outputs.file(outputFile)
        doLast {
            outputFile.text = templateModJson.text.replaceAll("module_version", "${project.version}").replaceAll("module_id", "sandbox-${project.name}")
        }
    }
    sourceSets.main.output.dir ext.genOutputDir, builtBy: generateFabricModJson

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version

                artifact jar
                artifact sourcesJar
            }
        }

        repositories {
            maven {
                url = rootProject.version.endsWith("-SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username ENV.MAVEN_USER
                    password ENV.MAVEN_PASS
                }
            }
        }
    }
}

repositories {
    maven { url 'https://nexus.sandboxpowered.org/repository/maven-public/' }
}

dependencies {
    constraints {
        api project(":base")
        api project(":rendering")

        api project(":resources")
    }
}

publishing {
    publications {
        platform(MavenPublication) {
            groupId = project.maven_group
            artifactId = 'api'

            from components.javaPlatform
        }
    }

    repositories {
        maven {
            url = rootProject.version.endsWith("-SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username ENV.MAVEN_USER
                password ENV.MAVEN_PASS
            }
        }
    }
}