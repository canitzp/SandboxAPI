plugins {
    id 'java'
    id 'maven-publish'
    id 'java-library'
}


sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = 'sandbox-api'
version = "${project.api_version}-SNAPSHOT"
group = project.maven_group

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    shade 'com.electronwill.night-config:core:3.6.0'
    shade 'com.electronwill.night-config:toml:3.6.0'
    shade 'com.github.zafarkhaja:java-semver:0.9.0'
    implementation group: 'com.google.guava', name: 'guava', version: '21.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
}

jar {
    from {
        configurations.shade.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourcesJar
        }
    }
    repositories {
        if (System.getenv("BRANCH") == "master") {
            maven {
                url "s3://cdn.hrzn.studio/maven"
                authentication {
                    awsIm(AwsImAuthentication)
                }
            }
        } else {
            mavenLocal()
        }
    }
}